/FORM1                                 * * * 08/28/86 * * * SYSTSCRN.SRC * * *
System Information                     ________________________________________
===============================================================================





               Program to Run On Exit  ____________________
                        Menu Time Out  _.
        Use Password Protection (Y/N)  _
                     Date from System  _
/*

INTEGER USER_NUMB 
INTEGER FROM_MENU
#IFDEF  NEXT_MENU
#ELSE
INTEGER NEXT_MENU
#ENDIF

AUTOPAGE FORM1

NAME  REGIST_1   EXIT_PROG  TIME_OUT   PASSWORD  SYST_DATE                  


STRING RESPONSE 1  
STRING PROMPT 76  

INDICATOR  OK    
INDICATOR  USE_PASS   

OPEN SYSTEM

INDICATE USE_PASS AS SYSFILE.PASSWORD EQ 'Y'

[USE_PASS] IF USER_NUMB LT 1 RUNPROGRAM SYSFILE.EXIT_PROG

STRING REGI_TEMP$  
STRING REGI_NAME$  
STRING RJ$1  
STRING RJ$2  
STRING REGI$TER
NUMBER S$ERL_NUMB

REGISTRATION REGI_NAME$ S$ERL_NUMB
TRIM REGI_NAME$ TO REGI_NAME$
MOVE '' TO REGI_TEMP$
MOVE '                              ' TO REGI_TEMP$
APPEND REGI_TEMP$ REGI_NAME$
RIGHT REGI_TEMP$ TO REGIST_1 40

PAGE FORM1

STRTFORM1:

ENTERGROUP

   ENTDISPLAY SYSTEM

   ENTRY SYSFILE.EXIT_PROG     EXIT_PROG  {CAPSLOCK}
   ENTRY SYSFILE.TIME_OUT      TIME_OUT
   ENTRY SYSFILE.PASSWORD      PASSWORD   {CAPSLOCK,CHECK='YN'}
   ENTRY SYSFILE.SYSTEM_DATE   SYST_DATE  {CAPSLOCK,CHECK='YN'}

ENDGROUP

   GOSUB SAVFORM1
   GOTO  STRTFORM1

SAVFORM1:

   MOVE '     Save this Information (Y/N)' TO PROMPT
   GOSUB SURE
[NOT OK] BEGIN
      CLEARFORM EXIT_PROG THRU SYST_DATE
      RETURN STRTFORM1
   END
[MULTIUSER] LOCK
   SAVERECORD SYSTEM
[MULTIUSER] UNLOCK
   CLEARFORM EXIT_PROG THRU SYST_DATE
   RETURN STRTFORM1

SURE:

   GOTOXY 23 0
   SHOW PROMPT

YESNO:

   INKEY RESPONSE
   IF NOT RESPONSE IN "YNyn" GOTO YESNO
[KEY.RETURN]  GOTO YESNO
   INDICATE OK AS RESPONSE IN "Yy"
   CLEARXY 23 0
   RETURN

KEYPROC KEY.SAVE

   GOTO SAVFORM1

KEYPROC KEY.DELETE

   MOVE '     Are you SURE you wish to Delete This (Y/N) ? ' TO PROMPT
   GOSUB SURE
[NOT OK] BEGIN
      ENTAGAIN
      RETURN
   END
[MULTIUSER] REREAD
   DELETE SYSTEM
[MULTIUSER] UNLOCK
   CLEARFORM EXIT_PROG THRU SYST_DATE
   RETURN STRTFORM1

KEYPROC KEY.ESCAPE

   CLEARXY 23 0
   GOTOXY 23 0
   MOVE '     Leaving This Screen (Y/N)? ' TO PROMPT
   GOSUB SURE
[NOT OK] BEGIN
      ENTAGAIN
      RETURN
   END
   ABORT

KEYPROC KEY.FIELD           

   BACKFIELD
   RETURN

KEYPROC KEY.CLEAR

   MOVE '     Clear this Screen (Y/N)? ' TO PROMPT
   GOSUB SURE
[NOT OK] BEGIN
      ENTAGAIN
      RETURN
   END
   CLEARFORM EXIT_PROG THRU SYST_DATE
   RETURN STRTFORM1
