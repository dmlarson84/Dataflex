 /HEADER
ÍÍÍÍÍÍÍÍÍ< __  ________________________ >ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
                L E D G E R   J O U R N A L    D E T A I L
For District:  __   ________________________                    Page: __.
Date Printed: __/__/____                                  Print Time: _.:_.
ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
   Date    G/L Dis Div      Description       Def#  Off#   Rec#     Amount
/subhead
ÍÍÍÍÍÍÍÍÍ< __  ________________________ >ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
   Date    G/L Dis Div      Description       Def#  Off#   Rec#     Amount

/BODY RESIDENT
_./_./___. ____.-___-__ _____________________ ____Z. ___Z. _____Z. _,___,___.__
/subtotal3
                                                                   ÄÄÄÄÄÄÄÄÄÄÄÄ
                          Day Out Of Balance By **************     _,___,___.__
                      ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
/subtotal2
                                                                   ÄÄÄÄÄÄÄÄÄÄÄÄ
                      Month Out Of Balance By ****************     _,___,___.__
                      ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
/Subtotal1
                                                                   ÄÄÄÄÄÄÄÄÄÄÄÄ
                      Journal Out Of Balance By **************     _,___,___.__
                      ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
/TOTAL
     Total Of All Selected and Printed Journals Out By *******     _,___,___.__

RECORDS PRINTED = ______.
/back
Hot Check Division              District __    [ ___________________________ ]
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

/SELECTION
ÚÄÄÄ< Record Selection >ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³                                                                   ³
³Enter the starting ................{ Month __. Day __. Yr____.     ³
³  Enter the ending month.............{ Month _. Day _. Yr ____.    ³
³                                                                   ³
³  Enter the beginning journal to print.....{ __                    ³
³  Enter the ending journal to print........{ __                    ³
³                                                                   ³
³  Enter District Number....................{ __                    ³
³  Enter Division Number....................{ __                    ³
³                                                                   ³
³                                                                   ³
³   Output To:   P)rinter  C)onsole  A)bort ...........{ _          ³
³                                                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/ques1
ÚÄÄÄÄ<Ques1>ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³                                                                   ³
³   Do you wish to print more journal records?  Y)es N)o ..[ _ ]    ³
³                                                                   ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/info
  Program created on 10-24-94 by Gary Grooms
  Purpose= To remove journal print routine from the ledger program
           in order to shorten the ledger program and to allow us to
           chain from the ledger program to this program in order to use
           the report macro.

  Change Made 11-29-94 By Gary Grooms
           Increased display of Dataflex record number to 6 places
           Was printing only 5 places

  Change 3-2-99 by Herta Jesse
        Modified program to comply with y2k,4 digit years and changed
        save routines to comply with v3.1d re-read, dave, unlock etc.
        edited the sysdate command to sysdate4.

  Change 10-28-99 by Jeff March
        Added direct output to LPT1:
        
Recompiled on 01-19-2000 by Jeff March
        Recognize backspace key in calcdate.inc


/*
Use y2k.pkg
Read_dfini "df23ini.cfg"
//----------------- Cannot Run Program From Command Line (DOS)
IF NEXT_MENU LE 0 ABORT
INTEGER USER_NUMB USER$_ACCESS   //must be first integers defined in program
STRING USER$ 30
MOVE '' TO USER$
MOVE 0 TO USER$_ACCESS
//------------------------------------------------------------
#include Calcdate.inc
#include macs.frm
//-------------- Find Logged On User For Program Use
OPEN USERX
CLEAR USERX
MOVE USER_NUMB TO USERX.USER_NUMB
FIND EQ USERX BY INDEX.1
MOVE USERX.USER_NAME TO USER$
MOVE USERX.ACCS_LEVL TO USER$_ACCESS
//----------------------------------------------------

//====< Setup >===========================================================



Date Today start$date end$date

integer  aday amonth ayear  bday bmonth byear  recnum$
Number accum1 accum2 accum3 accum4

String mo 2 day 2 yr 4  akey 1 answ 1  bell 1 jr 2
String opt1 1 Defname 25

Indicator mo$ day$  jr$  prt

Page set selection at 4 4 colors 63 63
Page set ques1 at 10 4 colors 63 63

Open setup
Open jrlist 1
Open Journals 3
Open Ledger
Open menu

Sysdate4 Today

DATE_MACRO_INIT
DECOMPOSE_DATE TODAY TO ADAY AMONTH AYEAR

 Move (0) to accum1
 character 7 to bell

find_setup:
    clear setup
    move ayear to setup.year
    find eq setup by index.1

Display setup.dist to back.1
Display setup.prosname to back.2
  Centerfield back.2 to back.2

Top:
  Page Back
  PAGE SELECTION
    Format selection.1 thru selection.11       {Capslock,Required}
 repeat
   repeat
     Clearform selection.1 thru selection.11
     Screenmode 63 on
        ACCEPT SELECTION.1
         Move (1) to selection.2
        ACCEPT SELECTION.2
        Display ayear to selection.3
        ACCEPT SELECTION.3
         Compose_date selection.2 selection.1 selection.3 to start$date
         Last_day_of_month start$date to end$date
         decompose_date end$date to bday bmonth byear
         Display bmonth to selection.4
         Display bday to selection.5
         Display ayear to selection.6
        ACCEPT SELECTION.4
        ACCEPT SELECTION.5
        ACCEPT SELECTION.6
        Display "AA" to selection.7
        ACCEPT SELECTION.7
          if selection.7 ne "AA" display selection.7 to selection.8
            else Display "ZZ" to selection.8
        ACCEPT SELECTION.8
          If selection.8 lt selection.7 Move "ZZ" to selection.8
        Display setup.dist to selection.9
        ACCEPT SELECTION.9
        Display "20" to selection.10
        ACCEPT SELECTION.10
        Display "P" to selection.11
        ACCEPT SELECTION.11 to opt1
//Show bell
  gotoxy 24 0
  clearxy 24 0
  repeat
    Screenmode 112 on
    Show "                    Are your entries correct? (Y/N)                            "
    gotoxy 24 54
    inkey answ
      uppercase answ
      clearxy 24 0
      if ~ answ in "YN" loop
  Screenmode 112 off
   if answ eq "N" Loop
  Screenmode 1 on
  gosub select_output
//Rptprms

Move (0) to recnum$

  INDICATE NOT JUMPIN AS "?" IN SELECTION.8
  [JUMPIN] Begin
     Move selection.1 to journals.mn
     Move selection.2 to journals.dy
     Move selection.3 to journals.yr
     Move selection.7 to journals.jr
     MOVE SELECTION.10 TO JOURNALS.DP //JUMP INTO INDEX
  end
    Move "" to defname
//  [JUMPIN] REPLACE "*" IN JOURNALS.DP WITH " "

REPORT JOURNALS BY INDEX.3 Break Journals.jr Journals.Mn Journals.dy
             INDICATE SELECT AS SELECTION.10 MATCH JOURNALS.DP
[    Select] INDICATE SELECT AS JOURNALS.JR GE SELECTION.7
[    SELECT] INDICATE SELECT AS JOURNALS.JR LE SELECTION.8
[JUMPIN][NOT SELECT] RETURN END.OF.REPORT  //JUMP OUT OF INDEX
[    Select] INDICATE SELECT AS JOURNALS.MN GE SELECTION.1
[    Select] Indicate select as journals.mn le selection.4
[    SELECT] INDICATE SELECT AS JOURNALS.DY GE SELECTION.2
[    SELECT] INDICATE SELECT AS JOURNALS.DY LE SELECTION.5
             increment recnum$
         [prt] begin
             gotoxy 12 18
             Show " Searching For Selected Records   "  recnum$
         end
[NOT SELECT] RETURN

   indicate jr$ as  journals.jr ne jr
   indicate mo$ as  journals.mn ne mo
   indicate day$ as  journals.dy ne day


SECTION HEADER
          Clear jrlist
          Move Journals.jr to jrlist.code
            Find eq jrlist by index.1
        Sysdate4 header.6 header.7 header.8
        Print Jrlist.code to header.1
        Print Jrlist.code to subhead.1
        Print Jrlist.Desc to header.2
        Print Jrlist.Desc to subhead.2
        Print Setup.dist to header.3
        Print Setup.Prosname to header.4
        Print Pagecount to header.5
        [prt] OUTPUT HEADER
        [~ prt] output subhead
SECTION BODY
             Indicate ok as defname ne "DEFENDANT CREDIT"
        PRINT JOURNALS.MN     to body.1
        PRINT JOURNALS.DY     to body.2
        PRINT JOURNALS.YR     to body.3
        PRINT JOURNALS.GL     to body.4
        PRINT JOURNALS.BR     to body.5
        PRINT JOURNALS.DP     to body.6
        if defname eq Journals.desc print " "  to body.7
        Else Print Journals.Desc                            to body.7
//        PRINT JOURNALS.DESC
        PRINT JOURNALS.EMPLY   to body.8
        PRINT JOURNALS.JOB     to body.9
        PRINT JOURNALS.RECNUM  to body.10
        PRINT JOURNALS.AMOUNT  to body.11
          move (Journals.amount + accum1) to accum1
          move (Journals.amount + accum2) to accum2
          move (Journals.amount + accum3) to accum3
          move (Journals.amount + accum4) to accum4
          move journals.jr to jr
          move journals.mn to mo
          move journals.dy to day
            Move Journals.desc to defname
        OUTPUT BODY
Section Subtotal3 12
//      Subtotal Body.11
        print accum3 to subtotal3.1
        Output Subtotal3
        move (0) to accum3
Section Subtotal2 7
//      Subtotal Subtotal3.1
        print accum2 to subtotal2.1
        Output Subtotal2
        move (0) to accum2
Section Subtotal1 3
//      Subtotal Subtotal2.1
        print accum1 to subtotal1.1
        Output Subtotal1
        move (0) to accum1
        formfeed
        gosub newpage
SECTION TOTAL
//      SubTotal Subtotal3.1
        Print accum4 to total.1
        PRINT RECCOUNT TO TOTAL.2
        OUTPUT TOTAL
        move (0) to accum4
REPORTEND
despool
//     copyfile "jrnls.txt" to "LPT1:"
FORMFEED

page back
   repeat
      screenmode 63 on
      clearform ques1.1
      accept ques1.1 to answ
      uppercase answ
   if ~ answ in "YN" loop
if answ in "Y"  loop

chain 'ledger'
//abort

// ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ Select Output
select_output:
         indicate prt false

  resel: if opt1 eq 'C' begin
              Direct_Output 'CON:'
              move 24 to pageend
              move -1 to pagefeed
              indicate prt false
              goto endsel
         end
         if opt1 eq 'P' begin
              indicate prt true
              Direct_Output "journals.txt"
              move 60 to pageend
              move  0 to pagefeed
              goto endsel
         end
        if opt1 eq 'A' chain "ledger"
        // may need to place a chain back to ledger here

 endsel:

return


KeyProc Key.Escape
   show bell
 repeat
   gotoxy 24 0
   clearxy 24 0
   Screenmode 112 on
   show "                Are you sure you want to quit?  (Y/N)                        "
   gotoxy 24 54
   Inkey answ
     uppercase answ
       if ~ answ in "YN" loop
  screenmode 63 on
  if answ eq "Y"  chain 'ledger'
Return

KeyProc Key.Field
Keyproc Key.up
   Backfield
Return

KeyProc Key.clear
   clearform selection
   Screenmode 63 on
   return Top


KeyProc Key.Find
KeyProc Key.Sfind
KeyProc Key.Save
KeyProc Key.Delete
KeyProc Key.Next
KeyProc Key.Previous
KeyProc Key.Print
KeyProc Key.user
KeyProc Key.user2
KeyProc Key.help
   Entagain


