/PAGE1                                                      hotchk.frm
ษออ< Defendant / Offense Entry Screen >อออออออออออออออออออออออออออออออออออออออป
บ ID No: [_____.] Def. Name: [______________________________](Last, First MI) บ
บ Address: ____________________________ Address: ____________________________ บ
บ City...: __________________________          St: __    Zip: __________      บ
บ Identification: Physical (Y/N) _ Driver's License _________  SSN: _________ บ
บ Description: Sex _  Race _  DOB __/__/____   Height _.'_."  Weight __. lbs. บ
บ Other _____________________________________________________________________ บ
บ       _____________________________________________________________________ บ
บ Credit:______.__    Outstanding Offenses:______.__    Last Ctrl # Used:___. บ
ฬออ< Offense Section >ออออออออออออออออออออออออออออออออออออออออออออออออออออออออน
บ Control No: [_____.-___.]   Complaint No: ____-_____.                       บ
บ Merchant No: [_____.]      Name: [______________________________]           บ
บ Address: ______________________ City: _______________ St: __ Zip:__________ บ
บ Salutation: _________________________       Phone: ______________           บ
บ Accepted by: ______________________________                                 บ
บ Check Offense Date: __/__/____  Check #: ______                             บ
บ Merchant Fee: ___.__  Amount: ______.__ Pros.Fee: ___.__ Total:_________.__ บ
บ Bank: ______________________________  Location: ___________________________ บ
บ Reason Returned.....: <_>                                   Offense:  <_>   บ
บ Courtesy Letter Date: __/__/____                   Warrant Date: __/__/____ บ
บ Judge: ___  ___________________________  Active: _  Active Date: __/__/____ บ
บ Court Case No: __________  Entry Date: __/__/____   Warrant Override _      บ
ศอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
/HELP1
ษอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ                                                                             บ
บ               PRESS ANY KEY TO RETURN TO PREVIOUS SCREEN                    บ
บ                                                                             บ
บ               Defendant: ______________________________                     บ
บ                                                                             บ
บ    _____________________________________________________________________|   บ
บ    _____________________________________________________________________|   บ
บ    _____________________________________________________________________|   บ
บ    _____________________________________________________________________|   บ
บ    _____________________________________________________________________|   บ
บ    _____________________________________________________________________|   บ
บ    _____________________________________________________________________|   บ
บ    _____________________________________________________________________|   บ
บ    _____________________________________________________________________|   บ
บ    _____________________________________________________________________|   บ
บ    _____________________________________________________________________|   บ
บ    _____________________________________________________________________|   บ
บ                                                                             บ
ศอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ 
/CHANGES
                          WORTHLESS CHECK SYSTEM
                        Record Changing Entry Screen
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


            Operator: ___                       Date: __/__/__

            Changes: (What was changed and Why)

            _______________________________________________________ |
            _______________________________________________________ |
            _______________________________________________________ |

/NOSETUP
                         * * W A R N I N G * *

         Can't find a record in the HOT CHECK SETUP TABLE for ____ !!


    You MUST add a record to the Setup Table (option 7 on Master Menu)
    and/or check that the system date is correct.





          Press any key to continue _



/def2
ีออออออออ< Defendant >อออออออออออออออออออออออออออออออออออออออออออออออออออธ
ณ                                                                        ณ
ณ  ID #          Name                       DL     Race  Sex   SSN       ณ
ณ                                                                        ณ
ณ ______. ______________________________  _________  _    _   ________   ณ
ณ ______. ______________________________  _________  _    _   ________   ณ
ณ ______. ______________________________  _________  _    _   ________   ณ
ณ ______. ______________________________  _________  _    _   ________   ณ
ณ ______. ______________________________  _________  _    _   ________   ณ
ณ ______. ______________________________  _________  _    _   ________   ณ
ณ ______. ______________________________  _________  _    _   ________   ณ
ณ ______. ______________________________  _________  _    _   ________   ณ
ณ                                                                        ณ
ณ   Highlight The Defendant You Wish To View and Press <RETURN>          ณ
ณ   Use Up & Down Arrow Keys To Scroll Defendants                        ณ
ิออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออพ
/STAR
ีออออออออ< Enter Defendant's Last Name...... >อออออออออออออออออออออออออออธ
ณ                                                                        ณ
ณ  Please Enter The Defendant's Last Name:                               ณ
ณ                                                                        ณ
ณ                                                                        ณ
ณ                   < _________________________ >                        ณ
ณ                           ( last Name)                                 ณ
ณ                                                                        ณ
ณ OR, press < ENTER > to list defendants alphabetically                  ณ
ิออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออพ
/banks
ีออออออออ< Bank Lookup Table >ออออออออออออออออออออออออออออออออออออออออธ
ณ  ID #           Bank                        Location                ณ
รฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤด
ณ ______  _____________________________  ____________________________ ณ
ณ ______  _____________________________  ____________________________ ณ
ณ ______  _____________________________  ____________________________ ณ
ณ ______  _____________________________  ____________________________ ณ
ณ ______  _____________________________  ____________________________ ณ
ณ ______  _____________________________  ____________________________ ณ
ณ ______  _____________________________  ____________________________ ณ
ณ ______  _____________________________  ____________________________ ณ
รฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤด
ณ Use  Arrow Keys To Select,PgUp PgDn Keys for more Then <RETURN>   ณ
ิอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออพ

/STAND
____________________
/EXPLANATIONS
    *****  This Page will never be displayed to the screen  *****
   
  INTEGERS :    User_numb..........{ Must be first integer defined in each
                                     program.  User_numb keeps track of person
                                     logged on to system.
                




/WARN1
ษออออออออออออออออออออออออออออออออออออออออออออออป
บ   This offense has already been paid off     บ
บ   please use the Restitution/Disbursement    บ
บ   program to make any changes.               บ
บ          Press Space Bar to continue         บ
ศออออออออออออออออออออออออออออออออออออออออออออออผ
/WARN2
ษออออออออออออออออออออออออออออออออออออออออออออออป
บ   You have selected this offense to be set   บ
บ   Inactive!! Are you sure this is what you   บ
บ   want to do? (Y/N)                          บ
บ                                              บ
ศออออออออออออออออออออออออออออออออออออออออออออออผ
/WARN3
ษออออออออออออออออออออออออออออออออออออออออออออออป
บ   You have selected to reactivate this       บ
บ   offense !! Are you sure this is what you   บ
บ   want to do? (Y/N)                          บ
บ                                              บ
ศออออออออออออออออออออออออออออออออออออออออออออออผ
/warn4
ษอออ< warn4 >ออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ                                                                              บ
บ   Merchant ID cannot be changed on an existing offense.                      บ
บ   If the merchant is wrong on a recently-entered offense, delete the offense บ
บ   and reenter it with the correct merchant.                                  บ
บ                                                                              บ
บ                       Press <SPACE> To Continue                              บ
ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
/warn5
ษอออ< warn5 >ออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ                                                                              บ
บ   Merchant name cannot be changed on this screen.                            บ
บ   If a name change is required, add a new merchant to the Merchant Table     บ
บ   and Combine Two Merchants Into One as outlined in your manual.             บ
บ                                                                              บ
บ                       Press <SPACE> To Continue                              บ
ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
/warn6
ษอออ< warn6 >ออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ                                                                              บ
บ                                                                              บ
บ                      A Merchant ID is required.                              บ
บ                                                                              บ
บ                                                                              บ
บ                       Press <SPACE> To Continue                              บ
ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

/WARN7
ษออออออออออออออออออออออออออออออออออออออออออออออป
บ   Defendant name cannot be changed here.     บ
บ                                              บ
บ   Please refer to the Hot Check Manual.      บ
บ                                              บ
บ          Press Space Bar to continue         บ
ศออออออออออออออออออออออออออออออออออออออออออออออผ
/WARN8
ษอออ< warn8 >ออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ                                                                        บ
บ     You Cannot Access This Program Until Year End Has Been Processed   บ
บ                                                                        บ
บ      Ayear  ___.    Amonth _.      Aday _.     Today Is.. __/__/____   บ
บ                                                                        บ
บ                     Press <SPACE> To Continue                          บ
ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
/active_codes
ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บACTIVE CODES                                                         บ
บ space = active offense      L = do not print Letter,warrant/summons บ
บ B=declared Bankruptcy       M = withdrawn by Merchant request       บ
บ C=sent to the Court         N = paid                                บ
บ D = District remains active O = Circuit remains active              บ
บ F = Forgery                 P = Not Prosecutable                    บ
บ I = Inactive offense             Enter Your Choice.....[ _ ]        บ
ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
/EXIT            RESIDENT
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~










              Please Standby, Closing Files and returning to the Menu










/info

Changes made:
    03-05-91  by Gary Grooms
       added routine to prevent saving a merchant record with 0 id#
       added {required} to fields in the offense record to prevent
       saving an offense without the necessary info

    03-21-91  by Beth
         expanded control number to 3 digits
         added get_answ subroutine
         deleted warn4 which was a duplicate nosetup message

    03-27-91  by Beth
         added routine to prevent changing defendant's name.

    05-06-91 by grooms
         move the if active eq "N" routine in the enter.edit to
         below the ifchange set changemade to allow for edits
         of non dollar amounts as in edit the hotcheck daTE.

    05-08-95 By Grooms
        Removed Key Proc to allow transfering def inf from case management
        system

    8-1-95 BY HERTA JESSE
        INCREASE MFEE ALLOWED TO $20.00

    9-8-95 By Gary Grooms
        Added new Active Codes routine

    11-5-95 By Gary Grooms
        Added a warning screen to deny access to this program until
        Year end processing completed

    03-14-96 By Gary Grooms
        Modified the Inactive codes to Add a "P" for Not Prosecutable
        And fix the GL update as offenses are set yo inactive or reactivated


    01-28-1999 By Gary Grooms
        Modified program to comply with y2k - 4 digit years and changed
        save routines to comply with v3.1d re-read, save, unlock etc.
        edited the sysdate command to sysdate4 command (new V3.1d)

   06-11-99 By Gary Grooms
         Removed the "require 4 digit year routine after entry of check date"
         see commented out code lines.

        would not allow the operator to enter a prior year check.

Recompiled 01-19-2000 by Jeff March
        Recognize new backspace command in calcdate.inc
		

	07-27-2007 by Jeff March
		Legislative Change for felony hotchecks to $500.01	

	04-01-2009 By Jeff March
		Added WARRANT OVERRIDE field for web application.
		
	05-31-2011 By Jeff March		
		Added "D" to Active Status Codes to allow flag for sent to Court but offense
		active in GL 1250 & Offense in Process GL 2500
		
	07-11-2011 By Jeff March
		Legislative Change for increase in Merchant and Prosecutor Fees, plus change
		in Felony criteria increased to $1000.01

	02-24-2016 by Don Larson
		Removed references to Case Management. Includes all scroll bar subroutines, macros
			and keyprocs related. Renamed file from hotchk.std to hotchk.frm. May remove
			references to the Judge table later. Recompiled.
/*
Use y2k.pkg
Read_dfini "df23ini.cfg"

TOP:

//----------------- Cannot Run Program From Command Line (DOS)
IF NEXT_MENU LE 0 ABORT
INTEGER USER_NUMB USER$_ACCESS   //must be first integers defined in program
STRING USER$ 30
MOVE '' TO USER$
MOVE 0 TO USER$_ACCESS
//------------------------------------------------------------


#INCLUDE calcDate.inc
#INCLUDE macs.frm

//========< Declare Variables >==============================================

STRING ANSW 1 HOLDNO 5 YEAR 2 YR 4 JI 3 BLOC 30 spc Temp_Active 1
STRING USER 30 NEW_NAME 30 TEMP_LAST 25 TEMP_FIRST 20 TEMP_MID 2
STRING OLD_ACTIVE 1 bell 1
NUMBER MF OLD_TOTAL ledg_amt

INTEGER SW COUNTER UHC USER_ACCESS
INTEGER ADAY AMONTH AYEAR

INDICATOR P_CK NEWMERCH NEWDEF NEWOFF DEL_NOW CHANGE_MADE
INDICATOR PASS_CHANGE START$_NAME
INDICATOR DO$_CHANGE
INDICATOR NOGO INACT DEL_ACT REDO_DATE react Blank$

DATE TODAY

//==========< Initialize Variables >==========================================

SYSDATE4 TODAY
MOVE (0.00) TO OLD_TOTAL
MOVE 0 TO USER_ACCESS
MOVE '' TO USER

character 7 to bell
character 88 to spc

//===========< Page Sets >====================================================
Page set Page1 at 1 0
PAGE SET DEF2 at 6 6 colors 79 79
PAGE SET STAND AT 12 30 
PAGE SET banks at 3 4 colors 63 63
PAGE SET STAR AT 6 6 COLORS 79 79
PAGE SET WARN1 AT 10 10
PAGE SET WARN2 AT 10 15
PAGE SET WARN3 AT 10 15
page set warn4 at 10 0
page set warn5 at 10 0
page set warn6 at 10 0
page set warn7 at 10 10
page set warn8 at 10 4
page set active_codes at 10 5 colors 79 79


AUTOPAGE PAGE1 

//===========< Name Windows >=================================================
 
NAME DEF_ID  DEF_NAME DEF_AD1  DEF_AD2 DEF_CITY DEF_ST DEF_ZIP DEF_PHYS DEF_DL
//        1       2        3        4       5        6      7       8        9
NAME DEF_SSN DEF_SEX  DEF_RACE DEF_DOB DEF_FT   DEF_IN DEF_WT DEF_OTH1 DEF_OTH2
//       10      11       12       13      14       15     16     17       18
NAME DEF_CRED DEF_BAL DEF_LCTRL#
//       19       20     21

NAME OFF_ID   OFF_CTRL  OFF_YR   OFF_CMPLNT 
//       22       23        24       25
NAME MER_ID   MER_NAME  MER_AD   MER_CITY  MER_ST   MER_ZIP  MER_SAL  MER_PHONE 
//       26       27        28       29        30       31       32       33
NAME OFF_ACPT OFF_DATE  OFF_CK#  OFF_MFEE  OFF_AMT  OFF_PFEE OFF_TOTAL OFF_BANK
//       34       35        36        37       38        39       40      41
NAME OFF_LOC  OFF_REAS  OFF_OFF   OFF_LTDT OFF_WDT   OFF_JUDG JUD_JNAME
//       42       43        44        45       46        47       48    
NAME OFF_ACT  OFF_ACTDT OFF_CASE# OFF_ENTRY OFF_WARR
//       49       50        51        52      53    

//===========< Open Files >===================================================

MULTI$ OPEN DEFENDNT OFFENSE SETUP JUDGE RECHANGE MERCHANT
MULTI$ OPEN TRANSACT USERX REF JOURNALS
OPEN LEDGER INDEX.1
FORMAT CHANGES.1 THRU CHANGES.3 {REQUIRED}


DATE_MACRO_INIT
DECOMPOSE_DATE TODAY TO ADAY AMONTH AYEAR
//MOVE '19' TO YR
//APPEND YR AYEAR

if ref.current_yr ne AYEAR begin
   page warn8
   display ayear to warn8.1
   display amonth to warn8.2
   display aday to warn8.3
   display today to warn8.4
   inkey answ
   abort
end



gosub find_SETUP
IF SETUP.PROSNAME LE '' IF SETUP.LSTID LE 0 GOSUB NOSETUP
MOVE SETUP.JUDGE_INIT TO JI
MOVE SETUP.MFEE_DEFAULT TO MF
MOVE SETUP.LOC_DEFAULT TO BLOC


//==========< Track Logged On User and Their Access Level >===================

CLEAR USERX
MOVE USER_NUMB TO USERX.USER_NUMB
     FIND EQ USERX BY INDEX.1
     MOVE USERX.USER_NAME TO USER
     MOVE USERX.ACCS_LEVL TO USER_ACCESS
     MOVE USERX.HC_LEVEL TO UHC


//=========< Start Program >==================================================

ENTER OFFENSE DEFENDNT MERCHANT

 START:
    INDICATE NOGO FALSE
    INDICATE INACT FALSE
    indicate react false
    CLEARXY 24 0
    CLEAR RECHANGE TRANSACT
    AUTOPAGE PAGE1
    ENTRY DEFENDNT.IDNO                {AUTOFIND,RETAIN}
    IF DEF_ID EQ '' IF UHC LT 2 GOSUB NOT_AUTH
    IF DEF_ID EQ '' BEGIN
         CLEARXY 24 0
         GOTOXY 24 17
         SHOW 'Do you wish to assign a new ID number (Y/N)?'
         gosub get_ANSW
         CLEARXY 24 0
         SCREENMODE 1
         IF ANSW IN 'Yy' BEGIN
              gosub find_SETUP
              REREAD SETUP
                   MOVE (SETUP.LSTID + 1) TO DEF_ID
                   MOVE DEF_ID TO SETUP.LSTID
                   SAVERECORD SETUP
              UNLOCK      // UNLOCK FOR SETUP FILE REREAD ABOVE
         END
    END

START_NAME:
    ENTRY DEFENDNT.NAME                {AUTOFIND,RETAIN}
    if status defendnt if page1.2 ne defendnt.name begin
         page warn7
         inkey answ
         entdisplay defendnt
         goto entrysec
    end
    IF DEF_ID LE 0 BEGIN                //if no number in def_id and operator
         CLEARFORM DEF_ID THRU DEF_NAME //tries to pass the name field without
         GOTO START                     //moving a defendant record into the
    END                                 //buffer action will return to start

    ENTRY DEFENDNT.ADDR                {RETAIN,SKIPFOUND}
    ENTRY DEFENDNT.ADDR2               {RETAIN,SKIPFOUND}
    ENTRY DEFENDNT.CITY                {RETAIN,SKIPFOUND}
    IF DEF_ST EQ "" MOVE "AR" TO DEF_ST
    ENTRY DEFENDNT.ST                  {CAPSLOCK,RETAIN,SKIPFOUND}
    ENTRY DEFENDNT.ZIP                 {RETAIN,SKIPFOUND}
    INDICATE NEWDEF FALSE
    IF ~ STATUS DEFENDNT INDICATE NEWDEF TRUE
    [NEWDEF] GOTOXY 24 10
    [NEWDEF] SHOW 'Can the defendant be recognized on sight   Y=yes N=no'
    ENTRY DEFENDNT.PHYS                {CAPSLOCK,CHECK ="YN ",RETAIN,SKIPFOUND}
    CLEARXY 24 0
    ENTRY DEFENDNT.LIC                 {RETAIN,SKIPFOUND}
    ENTRY DEFENDNT.SOCSEC              {RETAIN,SKIPFOUND}
    [NEWDEF] GOTOXY 24 20
    [NEWDEF] SHOW 'M = male       F = female '
    ENTRY DEFENDNT.SEX                 {CAPSLOCK,CHECK ="MFU ",RETAIN,SKIPFOUND}  
    [NEWDEF] CLEARXY 24 0
    [NEWDEF] GOTOXY 24 10
    [NEWDEF] SHOW 'A=Asian  B=Black  H=Hispanic  I=Indian  W=White  X=Unknown'
    ENTRY DEFENDNT.RACE                {CAPSLOCK,CHECK = "ABHIWX ",RETAIN,SKIPFOUND}
    [NEWDEF] CLEARXY 24 0
    ENTRY DEFENDNT.AGE                 {RETAIN,SKIPFOUND}
    ENTRY DEFENDNT.FT                  {RANGE = 0,9,RETAIN,SKIPFOUND}
    ENTRY DEFENDNT.IN                  {RANGE = 0,11,RETAIN,SKIPFOUND}
    ENTRY DEFENDNT.LBS                 {RETAIN,SKIPFOUND}
    [NEWDEF] GOTOXY 24 10
    [NEWDEF] SHOW 'This first line of OTHER comments will appear on the warrant'
    ENTRY DEFENDNT.OTHER               {RETAIN,SKIPFOUND}
    [NEWDEF] CLEARXY 24 0
    ENTRY DEFENDNT.OTHER2              {RETAIN,SKIPFOUND}
    ENTRY DEFENDNT.CREDIT              {RETAIN,SKIPFOUND,NOENTER}
    ENTRY DEFENDNT.BAL                 {RETAIN,SKIPFOUND,NOENTER}
    ENTRY DEFENDNT.LAST_CTRL#          {RETAIN,SKIPFOUND,NOENTER}

  CONTROL_#:

    IF OFF_ID EQ '' MOVE DEF_ID TO OFF_ID
    ENTRY OFFENSE.IDNO                   {NOENTER}
    ENTRY OFFENSE.CTRLNO                 {AUTOFIND}
    [FOUND] MOVE OFFENSE.TOTAL TO OLD_TOTAL
    INDICATE NEWOFF FALSE
    IF ~ STATUS OFFENSE INDICATE NEWOFF TRUE
    [NEWOFF] MOVE AYEAR TO OFF_YR
    ENTRY OFFENSE.YEAR
    CLEARXY 24 0
    ENTRY OFFENSE.COMPLAINT_#
    if status offense indicate newoff false
  merchant$:
    ENTRY MERCHANT.IDNO                   {AUTOFIND}
    [finderr] move '' to mer_name
    IF MER_ID EQ '' BEGIN
         clearxy 24 0
         GOTOXY 24 17
         SHOW 'Assign a new Merchant ID number (Y/N)?'
         INKEY ANSW
         CLEARXY 24 0
         SCREENMODE 1
         IF ANSW IN 'Yy' BEGIN
              INDICATE NEWMERCH TRUE
              gosub find_setup
              reread
                   MOVE (SETUP.MERCHANT + 1) TO MER_ID
                   MOVE MER_ID TO SETUP.MERCHANT
                   SAVERECORD SETUP
              UNLOCK
         END
         ELSE INDICATE NEWMERCH FALSE
    END
    [~ NEWMERCH] IF ~ STATUS OFFENSE BEGIN
         GOTOXY 24 10
         SHOW 'Hit TAB on merchant name to "find" an existing merchant'
    END
    ENTRY MERCHANT.NAME                   {AUTOFIND}
    
    if status merchant if mer_id ne merchant.idno begin
         page warn4
         inkey answ
         entdisplay offense
    end

    if status merchant if mer_name ne merchant.name begin
         page warn5
         inkey answ
         entdisplay offense
    end

    if ~ status merchant if mer_id eq 0 begin
         page warn6
         inkey answ
         gosub clear_merch
    end

    [~ newmerch] if ~ status merchant  gosub clear_merch

    if mer_name eq " " clearxy 24 0
    if mer_name eq " " goto merchant$


    [~ NEWMERCH] IF ~ STATUS OFFENSE CLEARXY 24 0
    IF OFF_CTRL LE 0 INDICATE NEWCTRL TRUE
    ENTRY MERCHANT.ADDR                   {SKIPFOUND}
    ENTRY MERCHANT.CITY                   {SKIPFOUND}
    IF MER_ST EQ "" MOVE "AR" TO MER_ST
    ENTRY MERCHANT.ST                     {CAPSLOCK,SKIPFOUND}
    ENTRY MERCHANT.ZIP                    {SKIPFOUND}
    ENTRY MERCHANT.SALUTATION             {SKIPFOUND}
    ENTRY MERCHANT.PHONE                  {SKIPFOUND}
    ENTRY OFFENSE.TAKEN
    AUTOPAGE PAGE1 OFF_DATE

    REPEAT
         move '' to YR
         INDICATE REDO_DATE FALSE
         ENTRY OFFENSE.CKDT		{required}
         CLEARXY 24 0
          Right Off_Date to yr 4

       //  If yr NE Ayear Begin
       //       show bell
       //       GOTOXY 24 0
       //       SHOW 'You must enter a 4 digit Year!!! <PRESS SPACE BAR TO CONTINUE> '
       //       Inkey Answ
       //       Move "N" to Answ
       //       CLEARXY 24 0
       //       INDICATE REDO_DATE TRUE
       //  End

         IF OFF_DATE GT TODAY BEGIN
              CLEARXY 24 0
              GOTOXY 24 0
              SHOW " Date is greater than today!  Please renter"
              INDICATE REDO_DATE TRUE
              CLEARFORM OFF_DATE
         END
    [REDO_DATE] LOOP
      
   ENTRY OFFENSE.CKNO  
   INDICATE P_CK FALSE
   [NEWOFF] IF MF NE 0 BEGIN                             //set defaults for pros. fee
    	IF OFF_DATE LT "07/27/2011" BEGIN
		   MOVE 25.00 TO OFF_MFEE
		END
        ELSE BEGIN
           MOVE MF TO OFF_MFEE
        END		   
    END 
	
    ENTRY OFFENSE.MFEE                   {RANGE = 0.00,99.99}
    ENTRY OFFENSE.CKAMT                  {required,RANGE = 0.00, 9999999.99}

  PFEE_CK:
   [NEWOFF] BEGIN                             //set defaults for pros. fee
    	IF OFF_DATE LT "07/27/2011" BEGIN
			IF OFF_AMT GT   0.00 IF OFF_AMT LE 100.00 MOVE 15.00 TO OFF_PFEE
			IF OFF_AMT GT 100.00 IF OFF_AMT LE 300.00 MOVE 30.00 TO OFF_PFEE
			IF OFF_AMT GT 300.00 IF OFF_AMT LE 500.00 MOVE 50.00 TO OFF_PFEE
			IF OFF_AMT GT 500.00 MOVE 75.00 TO OFF_PFEE
	    END	 
		ELSE BEGIN
			IF OFF_AMT GT   0.00 IF OFF_AMT LE 100.00 MOVE 25.00 TO OFF_PFEE
			IF OFF_AMT GT 100.00 IF OFF_AMT LE 300.00 MOVE 45.00 TO OFF_PFEE
			IF OFF_AMT GT 300.00 IF OFF_AMT LE 500.00 MOVE 65.00 TO OFF_PFEE
			IF OFF_AMT GT 500.00 MOVE 90.00 TO OFF_PFEE
		END

 //        [NEWOFF] BEGIN
 //	          
 //	       	IF OFF_DATE LT "07/16/2003" BEGIN
 //	        		
 //	       		IF OFF_AMT LE 10.00 MOVE 5.00 TO OFF_PFEE
 //	       		IF OFF_AMT GT 10.00 IF OFF_AMT LE 100.00 MOVE 10.00 TO OFF_PFEE
 //	       		MOVE (OFF_MFEE+OFF_AMT+OFF_PFEE) TO OFF_TOTAL
 //	       	END
	          	
 //        END
	end        

    ENTRY OFFENSE.PFEE                  // {RANGE = 0.00,90.00}
	ENTRY OFFENSE.TOTAL                     {NOENTER}
	//   [NEWOFF] BEGIN
 //        INDICATE P_CK FALSE
 //        //IF OFF_AMT LE 10.00 IF OFF_PFEE GT 5.00 INDICATE P_CK TRUE
 //        IF OFF_DATE LT "07/16/2003" BEGIN
         
 //        	IF OFF_AMT LE 10.00 IF OFF_PFEE GT 5 INDICATE P_CK TRUE
 //        	IF OFF_AMT GT 10.00 IF OFF_AMT LE 100.00 IF OFF_PFEE  GT 10 INDICATE P_CK TRUE
         	
         	
 //        END
 //        ELSE BEGIN
 //        	IF OFF_AMT LE 100.00 IF OFF_PFEE GT 15.00 INDICATE P_CK TRUE	
 //        END
 //        [~P_CK] IF OFF_AMT LE 300.00 IF OFF_PFEE GT 30.00 INDICATE P_CK TRUE
 //        [~P_CK] IF OFF_AMT LE 500.00 IF OFF_PFEE GT 50.00 INDICATE P_CK TRUE
 //        [~P_CK] IF OFF_PFEE GT 75.00 INDICATE P_CK TRUE
 //        [ P_CK] BEGIN
 //             GOTOXY 24 1
 //             SHOW 'PROS. FEE CANNOT BE MORE THAN ALLOWED BY LAW'
 //             GOTO PFEE_CK
 //        END
    
 //   CLEARXY 24 0

    MOVE (OFF_MFEE+OFF_AMT+OFF_PFEE) TO OFF_TOTAL
    
        Clearmessage

//     [newoff] begin
//          scroll banks 8 3 setbank index.1 dispbank banklt bankgt banktest
//            [key.return]  begin
//                 calc ((scroll$l$count - 1 ) * 3) to windowindex
//                 move banks.2& to off_bank
//                 Display banks.3& to off_loc
//                 clear setbank
//                 goto pass_bank
//            end
//     end

    ENTRY OFFENSE.BANK
pass_bank:
    [NEWOFF] MOVE BLOC TO OFF_LOC
    ENTRY OFFENSE.LOC
    GOTOXY 24 1
    SHOW 'A=Acct Closed  I=Insufficient Funds  N=No Acct  S=Stop Pmt  F=Forgery'
    ENTRY OFFENSE.REASON                    {required,CAPSLOCK,CHECK='AIFNS'}
    CLEARXY 24 0
	[NEWOFF] IF OFF_DATE GE "7/27/2011" BEGIN
				IF OFF_AMT GT 1000.00 MOVE 'H' TO OFF_OFF
				IF OFF_AMT LE 1000.00 MOVE 'M' TO OFF_OFF
			 END
	         ELSE IF OFF_DATE GE "7/31/2007" BEGIN	
				IF OFF_AMT GT 500.00 MOVE 'H' TO OFF_OFF
				IF OFF_AMT LE 500.00 MOVE 'M' TO OFF_OFF
			 END
	         ELSE IF OFF_DATE LT "7/31/2007" BEGIN	
				IF OFF_AMT GT 200.00 MOVE 'H' TO OFF_OFF
				IF OFF_AMT LE 200.00 MOVE 'M' TO OFF_OFF
			 END
	GOTOXY 24 12
    SHOW ' H = Felony    M = Misdemeanor    W = Warm Check'
    ENTRY OFFENSE.OFFEN                     {CAPSLOCK,required,CHECK='HMW'}
    CLEARXY 24 0
    AUTOPAGE PAGE1 OFF_LTDT
    ENTRY OFFENSE.LETDT                     {NOENTER}
    ENTRY OFFENSE.WARR_DATE                 {NOENTER}

  FIND_JUDGE:
    [NEWOFF] MOVE JI TO OFF_JUDG
    ENTRY JUDGE.INIT                   {CAPSLOCK,AUTOFIND}
    [FINDERR] BEGIN
         CLEARXY 24 0
         GOTOXY 24 21
         SHOW 'This judge was not found.  Try again.'
         ENTRY JUDGE.NAME                   {NOPUT}
    END
        //++++  If offense has been paid, do not allow entry in offense.active.
        //++++  If offense is paid, changes to its status cannot be made 
        //++++  unless any disbursement & receipts are dealt with first.

    IF [~ NEWOFF] IF OFFENSE.ACTIVE IN 'N' GOTO PASS_ACTIVE
    CLEARXY 24 0
    SCREENMODE 1
    GOTOXY 24 10
    SHOW 'L=no letter or warrant     I=inactive     N=not active (paid)'

       //++++  USE OF THE KEY-FIELD AND THE KEY-UP INDICATORS PREVENTS
       //++++  THE OPERATOR FROM GOING PAST THE ACTIVE FIELD AND THEN 
       //++++  BACKING UP TO CHANGE IT.

      //++++  Store offense active in temp string old_active for later
      //++++  use in comparing buffer to screen for any change.


//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////

    [~ NEWOFF] MOVE OFFENSE.ACTIVE TO OLD_ACTIVE
      If off_act ne "N" begin
          Repeat
            Clearform Active_Codes
            move '' to active_codes.1
               [~ key.field][~ key.up] Accept Active_Codes.1
              Uppercase active_codes.1
              move active_codes.1 to off_act

              If off_act eq '' Begin
                   move temp_active to off_act
                   move temp_active to offense.active
                   goto pass_active
                 end
           if ~ temp_active in "BCDFILMP " Loop
               Move active_codes.1 to Off_Act

      end
    [~ KEY.FIELD][~ KEY.UP] ENTRY OFFENSE.ACTIVE     {CAPSLOCK,CHECK = "BCDFILMOP  "}
    Page page1
  PASS_ACTIVE:


      //++++ Routine to inactivate an offense

    CLEARXY 24 0
//    [~ newoff] if old_active in "BCFIM" goto finis
                                                       // remarked out
                                                       // Problem not updating
                                                       //gl when set to BCFIMP
                                                       //These are deactive codes

//    [~ NEWOFF] IF OFF_ACT in 'BCFIMP ' IF OFF_ACT NE OLD_ACTIVE BEGIN
      // Changed to command below to allow for gl update when change made

//      [~ NEWOFF] IF OFF_ACT in 'LBCFIMP ' IF Old_Active In 'LBCFIMP ' goto Finis
      [~ NEWOFF] IF OFF_ACT in 'BCFIMP' IF Old_Active In 'DL ' Begin
         PAGE WARN2
         gotoxy 13 38
         gosub get_ANSW
         IF ANSW EQ 'N' BEGIN
              MOVE OLD_ACTIVE TO OFF_ACT
              PAGE PAGE1
              GOTO PASS_ACTIVE
         END
         IF ANSW EQ 'Y' GOSUB INACT_OFF
         INDICATE CHANGE_MADE TRUE
         INDICATE INACT AS OFF_ACT IN 'BCFIMP'
         MOVE TODAY TO OFF_ACTDT
         GOTO FINIS
    END

       //++++ Routine to reactive an offense

    [~ NEWOFF] IF OLD_ACTIVE IN 'BCFIMP' IF NOT OFF_ACT in 'BCFIMP' begin  //NE OLD_ACTIVE BEGIN

         PAGE WARN3
         gotoxy 13 38
         gosub get_ANSW
         IF ANSW EQ 'N' BEGIN
              MOVE OLD_ACTIVE TO OFF_ACT
              PAGE PAGE1
              GOTO START       
         END
         IF ANSW EQ "Y" BEGIN
              gosub react_off
              INDICATE CHANGE_MADE TRUE
              move today to off_actdt
              GOTO FINIS
         END
    END

    IF OFF_ACTDT EQ ''  MOVE TODAY TO OFF_ACTDT
    ENTRY OFFENSE.ACTIVE_DATE          
    ENTRY OFFENSE.CT_CASE
    IF OFF_ENTRY EQ '' MOVE TODAY TO OFF_ENTRY
    ENTRY OFFENSE.ENTRY_DATE           {NOENTER}
    ENTRY OFFENSE.WARRANT_OVER		{NOENTER}

FINIS:

RETURN
 
ENTER.DELETE:
    IF UHC LT 4 GOSUB NOT_AUTH2
    IF UHC LT 4 ABORT
    IF OFFENSE.ACTIVE IN 'IN' BEGIN
         PAGE WARN1
         INKEY ANSW
         CLEAR OFFENSE
         CLEARFORM PAGE1.22 THRU PAGE1.52
         PAGE PAGE1
         RETURN RETURN START
    END
    INDICATE DEL_NOW TRUE
    INDICATE CHANGE_MADE TRUE 

    MOVE OFFENSE.MFEE TO RECHANGE.OLD_MFEE
    MOVE OFFENSE.CKAMT TO RECHANGE.OLD_PFEE
    MOVE OFFENSE.PFEE TO RECHANGE.OLD_CKAMT 

    MOVE 0 TO RECHANGE.NEW_MFEE
    MOVE 0 TO RECHANGE.NEW_PFEE
    MOVE 0 TO RECHANGE.NEW_CKAMT
RETURN

ENTER.EDIT:
    IF UHC LT 3 GOSUB NOT_AUTH
    IF UHC LT 3 ABORT        

    MOVE OFFENSE.MFEE TO RECHANGE.OLD_MFEE
    IFCHANGE OFF_MFEE INDICATE CHANGE_MADE TRUE
    [CHANGE_MADE] MOVE ( OFF_MFEE - OFFENSE.MFEE ) TO TRANSACT.MFEE
    [NOT DEL_NOW]  MOVE OFF_MFEE TO RECHANGE.NEW_MFEE
    MOVE OFFENSE.CKAMT TO RECHANGE.OLD_CKAMT
    IFCHANGE OFF_AMT INDICATE CHANGE_MADE TRUE
    [CHANGE_MADE] MOVE ( OFF_AMT - OFFENSE.CKAMT ) TO TRANSACT.CKAMT
    [NOT DEL_NOW]  MOVE OFF_AMT TO RECHANGE.NEW_CKAMT
    MOVE OFFENSE.PFEE TO RECHANGE.OLD_PFEE 
    IFCHANGE OFF_PFEE INDICATE CHANGE_MADE TRUE
    [CHANGE_MADE] MOVE ( OFF_PFEE - OFFENSE.PFEE ) TO TRANSACT.PFEE
    [NOT DEL_NOW]  MOVE OFF_PFEE TO RECHANGE.NEW_PFEE
    INDICATE NEWCTRL FALSE
    INDICATE DO$_CHANGE GROUP ALL [NOT NEWDEF NOT NEWOFF]

    [change_made] IF OFFENSE.ACTIVE EQ 'N' BEGIN
         PAGE WARN1
         INKEY ANSW
         CLEAR OFFENSE
         CLEARFORM PAGE1.22 THRU PAGE1.52
         PAGE PAGE1
         RETURN RETURN START
    END
    [CHANGE_MADE] INDICATE PASS_CHANGE TRUE
    [CHANGE_MADE] GOSUB EDIT_OFF
    [CHANGE_MADE] GOSUB RECORD_CHANGE
    [CHANGE_MADE] GOSUB RECORD_TRANSACT
    [DEL_NOW] GOSUB DEL_OFF
    INDICATE DEL_NOW FALSE
RETURN

ENTER.SAVE:
    IF UHC LT 2 GOSUB NOT_AUTH
    IF UHC LT 2 ABORT
    [NOT NEWOFF] [NOT PASS_CHANGE] IFCHANGE OFFENSE  GOSUB RECORD_CHANGE
    INDICATE PASS_CHANGE FALSE
    INDICATE CHANGE_MADE FALSE

   [NEWCTRL] BEGIN
   //REREAD DEFENDNT
    MOVE (DEFENDNT.LAST_CTRL# + 1) TO OFFENSE.CTRLNO
    END
    IF OFFENSE.CTRLNO GT DEFENDNT.LAST_CTRL# MOVE OFFENSE.CTRLNO TO ;
                                                           DEFENDNT.LAST_CTRL#
    [NEWOFF] IF OFF_ACT NE 'I'  BEGIN
         MOVE ((OFFENSE.CKAMT + OFFENSE.MFEE + OFFENSE.PFEE) + DEFENDNT.BAL);
                                                           TO DEFENDNT.BAL
    END                
    MOVE OFFENSE.CTRLNO TO OFF_CTRL
    IF DEFENDNT.LAST_CTRL# NE DEF_LCTRL# BEGIN
         MOVE DEFENDNT.LAST_CTRL# TO DEF_LCTRL#
         GOTOXY 24 10
         [~ INACT]  SHOW 'Last Control Number Used has been incremented'
    END
    [NEWOFF] IF OFF_ACT NE 'I' GOSUB NEW_JR
    //SAVERECORD DEFENDNT
    //UNLOCK
    //INKEY ANSW

    ENTDISPLAY DEFENDNT
//      reread defendnt
RETURN

ENTEREND
ABORT
 
//===========< Key Procs >===================================================

keyproc key.any
    [key.save]  if current_window le 27 entagain
    
  
   	AUTOPAGE HELP1
   	MOVE PAGE1.2 TO HELP1.1
   	MOVE DEFENDNT.OTHER TO HELP1.2
   	MOVE DEFENDNT.OTHER2 TO HELP1.3
   	MOVE DEFENDNT.OTHER3 TO HELP1.4
   	MOVE DEFENDNT.OTHER4 TO HELP1.5
   	MOVE DEFENDNT.OTHER5 TO HELP1.6
   	MOVE DEFENDNT.OTHER6 TO HELP1.7
   	MOVE DEFENDNT.OTHER7 TO HELP1.8
   	MOVE DEFENDNT.OTHER8 TO HELP1.9
   	MOVE DEFENDNT.OTHER9 TO HELP1.10
   	MOVE DEFENDNT.OTHER10 TO HELP1.11
   	MOVE DEFENDNT.OTHER11 TO HELP1.12
   	MOVE DEFENDNT.OTHER12 TO HELP1.13
   	
   	
   RETURN
    
    
return


KEYPROC KEY.ESCAPE

    CLEARXY 24 0
    GOTOXY 24 10
    SHOW 'Do you really want to return to the main menu (Y/N)?  '
    INKEY ANSW
    IF ANSW IN 'Yy' BEGIN
         page exit
         CLEARFORM PAGE1
         clear merchant offense defendnt
         ABORT
    END
    CLEARXY 24 0
    ENTAGAIN
RETURN

KEYPROC KEY.USER


    CLEARXY 24 0
  IF DEF_ID EQ '' BEGIN
       GOTOXY 24 1
       SHOW "Defendant record must be present to move to offense section."
       INKEY ANSW
       ENTAGAIN
       RETURN START
  END
  [~ NEWDEF] IF ~ STATUS DEFENDNT BEGIN
       GOTOXY 24 1
       SHOW 'Hit RETURN on Defendant ID before going to offense section. '
       INKEY ANSW
       ENTAGAIN
       RETURN START
  END
  [NEWDEF] BEGIN
       MOVE DEF_ID TO DEFENDNT.IDNO
       FIND EQ DEFENDNT BY INDEX.1
       [FOUND] ENTDISPLAY DEFENDNT
  END
  [~ newdef] if status defendnt return control_#

//scrl def2 8 6 defendnt index.1 display_def def_lt def_gt def_test
  entagain
RETURN

KEYPROC KEY.UP

	
    BACKFIELD 
RETURN

KEYPROC KEY.FIELD


    BACKFIELD
RETURN


 
RECORD_CHANGE:
    MOVE 'OFFENSE'      TO RECHANGE.FILE
    CLEARFORM CHANGES
    PAGE CHANGES
    MOVE USER_NUMB                     TO CHANGES.1
    move changes.1      to rechange.operator
    IF CHANGES.2 EQ '' MOVE TODAY      TO CHANGES.2
    move changes.2      to rechange.date
  EXPLAIN:
    CLEARXY 24 0
    [INACT] MOVE 'SET INACTIVE'        TO CHANGES.5
    [DEL_NOW] MOVE 'RECORD DELETED'    TO CHANGES.5
    ACCEPT CHANGES.3    TO RECHANGE.DESC1 {required}
    ACCEPT CHANGES.4    TO RECHANGE.DESC2
    if changes.5 gt '' goto bypass
    accept changes.5
BYPASS:
    move CHANGES.5      TO RECHANGE.DESC3
    MOVE OFF_ID         TO RECHANGE.ID_NUMBER
    MOVE OFF_CTRL       TO RECHANGE.CONTROL_NUMBER
    [CHANGE_MADE] BEGIN
         MOVE CHANGES.3           TO TRANSACT.DESC1
         MOVE CHANGES.4           TO TRANSACT.DESC2
         MOVE CHANGES.5           TO TRANSACT.DESC3
    END
    SAVERECORD RECHANGE
RETURN


RECORD_TRANSACT:
    MOVE TODAY                    TO TRANSACT.DATE
    MOVE OFF_ID                   TO TRANSACT.IDNO
    MOVE OFF_CTRL                 TO TRANSACT.CTRL_SEQ_NO
    MOVE PAGE1.2                  TO TRANSACT.DEF_NAME
    MOVE 'OFFENSE'                TO TRANSACT.FILE
    MOVE USER_NUMB                TO TRANSACT.OPERATOR
    INDICATE DEL_ACT GROUP ANY [INACT DEL_NOW]
    [DEL_ACT] BEGIN
         MOVE (-OFFENSE.MFEE)     TO TRANSACT.MFEE
         MOVE (-OFFENSE.PFEE)     TO TRANSACT.PFEE
         MOVE (-OFFENSE.CKAMT)    TO TRANSACT.CKAMT
    END
    [INACT] INDICATE DEL_NOW FALSE
    SAVERECORD TRANSACT
 RETURN 

find_setup:
    clear setup
    move Ayear to setup.year
    find eq setup by index.1
    [finderr] gosub nosetup
return

NOSETUP:
    MOVE YR TO NOSETUP.1
    ACCEPT NOSETUP.2
    ABORT
RETURN

clear_merch:
    clearform mer_id thru mer_phone
    clear merchant
    clearxy 24 0
    if status offense entdisplay offense
return merchant$

NOT_AUTH:
    CLEAR DEFENDNT OFFENSE MERCHANT SETUP JUDGE RECHANGE TRANSACT
    CLEARXY 24 0
    SCREENMODE 112
    GOTOXY 24 0
    SHOW 'Sorry, you are not authorized to Edit, Add or Save a record. '
    MOVE 0 TO COUNTER
    REPEAT
         INCREMENT COUNTER
    UNTIL COUNTER EQ 500
    SCREENMODE 1
    CLEARXY 24 0
    CLEARFORM
    INDICATE NOGO TRUE
RETURN            

NOT_AUTH2:
    CLEAR DEFENDNT OFFENSE MERCHANT SETUP JUDGE RECHANGE TRANSACT
    CLEARXY 24 0
    SCREENMODE 112
    GOTOXY 24 0
    SHOW 'Sorry, you are not authorized to delete a record. '
    MOVE 0 TO COUNTER
    REPEAT
         INCREMENT COUNTER
    UNTIL COUNTER EQ 500
    SCREENMODE 1
    CLEARXY 24 0
    CLEARFORM
    INDICATE NOGO TRUE
RETURN            

get_answ:
    repeat
         inkey answ
         uppercase answ
    if ~ answ in 'YN' loop
return

NEW_JR:
      // THIS ROUTINE IS LOCKED FROM WITHIN THE ENTER.SAVE (ENTER MACRO)

    IF OFFENSE.CKAMT GT 0 BEGIN
         Clear journals

         MOVE 'OF'                                         TO JOURNALS.JR
         MOVE REF.NEWOFF                                   TO JOURNALS.GL
         MOVE (OFFENSE.MFEE + OFFENSE.CKAMT+ OFFENSE.PFEE) TO JOURNALS.AMOUNT
         GOSUB GL_JRNLS

         Clear Journals

      //CREDIT ENTRY
         MOVE 'OF'                                         TO JOURNALS.JR
         MOVE REF.APPROCESS                                TO JOURNALS.GL
         MOVE (OFF_TOTAL * (-1))                           TO JOURNALS.AMOUNT
         GOSUB GL_JRNLS
    END
RETURN

REACT_OFF:
    // THIS ROUTINE IS COMPLETED PRIOR TO THE ENTER.SAVE ROUTINE SO ALL
    // AFFECTED FILES MUST BE REREAD PRIOR TO SAVING RECORDS

    IF OFFENSE.CKAMT GT 0 BEGIN
         MOVE 'RA'                                         TO JOURNALS.JR
         MOVE REF.NEWOFF                                   TO JOURNALS.GL
         MOVE (OFFENSE.MFEE + OFFENSE.CKAMT+ OFFENSE.PFEE) TO JOURNALS.AMOUNT
         GOSUB GL_JRNLS
        //CREDIT ENTRY
              MOVE 'RA'                                         TO JOURNALS.JR
              MOVE REF.APPROCESS                                TO JOURNALS.GL
              MOVE (OFF_TOTAL * (-1))                           TO JOURNALS.AMOUNT
              GOSUB GL_JRNLS
              reread defendnt
              MOVE (DEFENDNT.BAL + OFF_TOTAL) TO DEFENDNT.BAL
              SAVERECORD DEFENDNT
              unlock
              //ENTDISPLAY DEFENDNT
    END
RETURN

DEL_OFF:
   // THIS ROUTINE COMPLETED WITHIN THE ENTER.SAVE (ENTER MACRO) SO ALL RECORDS
   // ARE REREAD PRIOR TO SAVING

    IF ~ OFFENSE.ACTIVE IN 'N' BEGIN
         // DEBIT ENTRY
         MOVE 'IN'                                         TO JOURNALS.JR
         MOVE REF.APPROCESS                                TO JOURNALS.GL
         MOVE (OFFENSE.MFEE + OFFENSE.CKAMT+ OFFENSE.PFEE) TO JOURNALS.AMOUNT
              GOSUB GL_JRNLS

        //CREDIT ENTRY
              MOVE 'IN'                                         TO JOURNALS.JR
              MOVE REF.NEWOFF                                   TO JOURNALS.GL
              MOVE (OFFENSE.TOTAL * (-1))                       TO JOURNALS.AMOUNT
              GOSUB GL_JRNLS
              reread defendnt
              MOVE (DEFENDNT.BAL - OFF_TOTAL) TO DEFENDNT.BAL
              SAVERECORD DEFENDNT
         unlock
    END
RETURN

INACT_OFF:
      // THIS ROUTINE COMPLETED PRIOR TO THE ENTER.SAVE ROUTINE, ALL RECORDS
      // MUST BE REREAD PRIOR TO SAVING RECORDS

    IF ~ OFFENSE.ACTIVE IN 'N' BEGIN
         // DEBIT ENTRY
         MOVE 'IN'                                         TO JOURNALS.JR
         MOVE REF.APPROCESS                                TO JOURNALS.GL
         MOVE (OFFENSE.MFEE + OFFENSE.CKAMT+ OFFENSE.PFEE) TO JOURNALS.AMOUNT
              GOSUB GL_JRNLS

        //CREDIT ENTRY
              MOVE 'IN'                                    TO JOURNALS.JR
              MOVE REF.NEWOFF                              TO JOURNALS.GL
              MOVE (OFFENSE.TOTAL * (-1))                  TO JOURNALS.AMOUNT
              GOSUB GL_JRNLS
              reread defendnt
              MOVE (DEFENDNT.BAL - OFF_TOTAL) TO DEFENDNT.BAL
              SAVERECORD DEFENDNT
         UNLOCK
    END
RETURN

EDIT_OFF:
   // THIS ROUTINE REREAD IN ENTER.SAVE ROUTINE

    IF OFF_TOTAL EQ OFFENSE.TOTAL GOTO END_EDIT_OFF
    IF OFF_TOTAL GT OFFENSE.TOTAL BEGIN
         MOVE REF.NEWOFF               TO JOURNALS.GL
         MOVE (OFF_TOTAL-OLD_TOTAL)    TO JOURNALS.AMOUNT
    END
    ELSE BEGIN
         MOVE REF.APPROCESS            TO JOURNALS.GL
         MOVE (OLD_TOTAL - OFF_TOTAL)  TO JOURNALS.AMOUNT
    END

    MOVE 'OF'                          TO JOURNALS.JR

  //CREDIT ENTRY
    IF OFF_TOTAL GT OFFENSE.TOTAL BEGIN
         MOVE REF.APPROCESS            TO JOURNALS.GL
         MOVE (OLD_TOTAL - OFF_TOTAL)  TO JOURNALS.AMOUNT
    END
    ELSE BEGIN
         MOVE REF.NEWOFF               TO JOURNALS.GL
         MOVE (OFF_TOTAL - OLD_TOTAL)  TO JOURNALS.AMOUNT
    END
    MOVE 'OF'                          TO JOURNALS.JR
    MOVE ((DEFENDNT.BAL) + (OFF_TOTAL - OLD_TOTAL)) TO DEFENDNT.BAL
    // NEW DEFENDANT BALANCE WILL BE SAVED IN THE ENTER MACRO SAVE ROUTINE
    // ALL RECORDS REREAD IN ENTER MACRO ROUTINE

 END_EDIT_OFF:
RETURN

FIND_GL:

    MOVE JOURNALS.GL TO LEDGER.NUMBER
    MOVE JOURNALS.BR TO LEDGER.DISTRICT
    MOVE JOURNALS.DP TO LEDGER.DIVISION
    FIND EQ LEDGER BY INDEX.1
RETURN

UPDATE_GL:
    reread ledger
    MOVE (JOURNALS.MN - 1) TO FIELDINDEX
    MOVE (LEDGER.TYJAN& + JOURNALS.AMOUNT)  TO LEDGER.TYJAN&
    MOVE (LEDGER.TYTOTAL + JOURNALS.AMOUNT) TO LEDGER.TYTOTAL
RETURN

GL_JRNLS:
    MOVE AYEAR               TO JOURNALS.YR
    MOVE AMONTH              TO JOURNALS.MN
    MOVE ADAY                TO JOURNALS.DY
    MOVE REF.DISTRICT        TO JOURNALS.BR
    MOVE '20'                TO JOURNALS.DP
    MOVE DEFENDNT.NAME       TO JOURNALS.DESC
    MOVE DEFENDNT.IDNO       TO JOURNALS.EMPLY
    MOVE OFFENSE.CTRLNO      TO JOURNALS.JOB  
    MOVE (REF.REF_NUM + 1)   TO JOURNALS.CHK
    reread ref
    MOVE (REF.REF_NUM + 1) TO REF.REF_NUM
    SAVERECORD REF
    unlock
    GOSUB FIND_GL
    GOSUB UPDATE_GL
    SAVERECORD JOURNALS
    SAVE LEDGER
    unlock
    CLEAR JOURNALS LEDGER
RETURN

