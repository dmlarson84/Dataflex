/BODY  RESIDENT                      WARRANT3.RPT



                                                               ______ ______.__ 

                       W A R R A N T   O F   A R R E S T 
  
 
 
   STATE  OF  ARKANSAS                  IN THE _C_@_____ COURT OF THE _C_@__ OF
   County of ______________  ss.        _C________________________ 
 
   The State of Arkansas to any  Sheriff, Constable, Coroner,  Jailer, Marshal,
   or Policeman in the State of Arkansas, Greeting: 
 
        IT  APPEARING that there are  reasonable  grounds  for  believing  that
   ______________________________       ______________________________
   ______________________________       _______________   __
   ____________________________________________________________________________
   ____________________________________________________________________________
   has  willfully  committed  the  offense of ______________________ (Ark. Code
   Ann.  5-37-302  et.  seq.)  in  the  City  of  ________________   County  of
   ________________   State  of  Arkansas,   you  are  therefore  commanded  to
   arrest  ______________________________        and   bring   him/her   before
   _______________________________ Judge of the _________  Court  of the ______
   of _______________  Arkansas to be dealt with according to law.  Given under 
   my hand and seal of said Court,  this __ day of _________ ____ .
 
 
   Summon as Witnesses for the State:       

   ____________________________________    
   ____________________________________    ____________________________________
   ____________________________________    Judge(Clerk) of the _@_______ Court 
   ____________________________________    
   ____________________________________
   ____________________________________    ____________________________________
                                           Judge(Clerk) of the _@_______ Court 
_______________________________________________________________________________

   Filed _________________ ____       ____________________________
                                      Clerk of the _@_______ Court
_______________________________________________________________________________
   RETURN:                                     FEES:
          STATE OF ARKANSAS
                                               Service _______________ $_______
   County of _________________________
   I Certify that I served the within warrant  Return  _______________ $_______
   of arrest on the _____ day of ____________
   ____  at ______________  by then and there  Ferriage & Mileage_____ $_______
   taking  into my  custody the within  named
   __________________________________________  Summoning __ Witnesses_ $_______
   __________________________________________
   whose  body  I  have  now  brought  before  Total _________________ $_______
   ___________________________________  Judge
   of the  _________  Court  of the ______ of  ________________________________
   _________________________ this ____ day of              (Title)
   ______________________________, A.D., ____  ________________________________
/FELONY1  RESIDENT
                             I N F O R M A T I O N

   Before ______________________________                     DISPOSITION
    Judge of the ________ Court for the             Plea ______________________
     _@_____ of _________________________              
                                                    
         STATE OF ARKANSAS                          Check Amount ___________.__
               vs.                                  Merchant Fee ___________.__ 
         _C_____________________________            Prosecutor Fee _________.__
                                                    Court Costs ____________.__
   STATE OF ARKANSAS                                
   County of ______________    ss.                  Total       ____________.__
   

        Comes now _______________________________  Prosecuting  Attorney of the
   ___  Judicial  Circuit of Arkansas,  in the name,   by the  authority,   and
   on  behalf  of  the  State  of  Arkansas,   and   charges   said   defendant 
   _C_________________________ of the crime of violating Ark.Code.Ann.5-37-302,
   Obtaining  Property  With Check  Drawn On Insufficient  Funds,  committed as
   follows,  to-wit:   The said defendant, in _______________ County,  Arkansas
   did make, draw,  utter or deliver with the intent to defraud, a check, draft
   or order, specifically:
   Instrument
   Number    Amount   Date   Drawn On                  Paid to

   ______ $_____.__ __/__/____ _______________________ _@_______________________

   for the  payment of money,  knowing  at the  time  of such  making  drawing,
   uttering or delivering,  that the maker or drawer,  has not sufficient funds
   in,  or on deposit with,  such bank,  person,  firm,  or corporation for the
   payment of such check, draft or order, in full, and all other checks, drafts
   or orders upon such funds then outstanding, against the peace and dignity of
   the State of Arkansas.

            The  above  instrument  totals   $_____.__,   which  is
   ____________________________________________________________________________

        Pray a warrant from this Court to apprehend  and  bring  said defendant
   before  said court  to be dealt  with  according  to the law of the State of
   Arkansas.

   Witnesses for the State

   ____________________________________          ______________________________
   ____________________________________          Prosecuting Attorney
   ____________________________________
   ____________________________________
   ____________________________________      By: ______________________________
   ____________________________________          Deputy Prosecuting Attorney
_______________________________________________________________________________
   Subscribed and sworn before me this ___ day of __________, ____ .


                                                  _____________________________
                                                          Judge-Clerk
/MISDEMEANOR1 RESIDENT
                             I N F O R M A T I O N

   Before ______________________________                     DISPOSITION
    Judge of the District Court for the             Plea ______________________
     City of _________________________              
                                                    
         STATE OF ARKANSAS                          Check Amount ___________.__
               vs.                                  Merchant Fee ___________.__ 
         _C____________________________             Prosecutor Fee _________.__
                                                    Court Costs ____________.__
   STATE OF ARKANSAS                                
   County of ______________    ss.                  Total       ____________.__
   
        Comes now _______________________________  Prosecuting  Attorney of the
   ___  Judicial  Circuit of Arkansas,  in the name,  by the authority,  and on
   behalf  of  the   State   of   Arkansas,   and   charges    said   defendant
   _C_________________________ of the crime of violating Ark.Code.Ann.5-37-302, 
   Obtaining  Property  With Check  Drawn On Insufficient  Funds,  committed as
   follows,  to-wit:  The said defendant,  in _C_____________ County,  Arkansas
   did make, draw,  utter or deliver with the intent to defraud, a check, draft
   or order, specifically:
   Instrument
   Number    Amount   Date   Drawn On                  Paid to

   ______ $_____.__ __/__/____ _________________________ _@_______________________

   for the  payment of money,  knowing  at the  time  of such  making  drawing,
   uttering or delivering,  that the maker or drawer,  has not sufficient funds
   in,  or on deposit with,  such bank,  person,  firm,  or corporation for the
   payment of such check, draft or order, in full, and all other checks, drafts
   or orders upon such funds then outstanding, against the peace and dignity of
   the State of Arkansas.
        The above offense is a misdemeanor  for which the penalty is set out in
   Ark. Code Ann. 5-37-305 as follows:
        (a) First Offense.  A fine of not less than fifty dollars ($50.00)
            nor more than five hundred dollars  ($500.00)  or imprisonment
            in the county  jail  or  regional  detention  facility  not to 
            exceed thirty (30) days or both.
        (b) Second Offense.  A fine of not less than one hundred dollars
            ($100.00)  nor more than one thousand dollars ($1,000.00) or 
            imprisonment  in  the  county  jail  or  regional  detention
            facility not to exceed ninety (90) days or both.
        (c) Third or Subsequent Offense.  A fine [of] not less than two
            hundred dollars ($200.00) nor more than two thousand dollars
            ($2,000.00)  or imprisonment in the county  jail or regional
            detention facility not to exceed one (1) year or both.
   Pray a warrant from this Court to apprehend  and bring said defendant before
   said court to be dealt with  according to the law of the  State of Arkansas.

   Witnesses for the State                       ______________________________
   ____________________________________          Prosecuting Attorney
   ____________________________________          
   ____________________________________      By: ______________________________
   ____________________________________          Deputy Prosecuting Attorney
_______________________________________________________________________________
   Subscribed and sworn before me this ___ day of __________, ____ .

                                                  _____________________________
                                                          Judge-Clerk
/WITNESSES RESIDENT
___________________________________
___________________________________
___________________________________
/CHECK_LINES RESIDENT
______ $_____.__ __/__/____ __________________________ _@________________________ 
/SELECT
                           HOT CHECK SYSTEM
                        Warrant Printing Report



          Print Informations for misdemeanor offenses? (Y/N)  _

          Enter H to use handfed paper     _

          Enter L to use legal size paper  _

          Print today's date for date filed? (Y/N)  _




         PRESS ESC TO STOP PRINTING

/SELECT2



          Print the word 'Bond $' on warrants? (Y/N) _

/DATE_SCRN
__/__/____
/BADDT
                  SURPRISE     SURPRISE     SURPRISE !!!!!


       A problem in the offense data (or in the system date) keeps 

       the rest of the warrants and informations from printing.
              


         Id # ______.  Ctrl #  _.  has a check date of __/__/____  which is

         greater than or equal to today's date __/__/____    

         Fix this and run warrants again.



                        Press any key to continue 

/NOSETUP
                             * * W A R N I N G * *

         There is no record in the HOT CHECK SETUP TABLE for ____ !!


    You MUST add a record to the Setup Table (option 7 on the Master Menu)
    and/or check that the system date is correct.





                          Press any key to continue 
/INFO

   CHANGES MADE ON 10/05/1999 BY JEFF MARCH
        ADDED DIRECT_OUTPUT TO LPT1: FOR PRINTING OF WARRANTS.
		
   CHANGES MADE ON 08/06/2007 BY JEFF MARCH
		ADDED LEGISLATIVE CHANGE REGARDING FELONIES GT 500.00

	CHANGES MADE ON 07/22/2011 BY JEFF MARCH
        Changed warrant for increase in felony amount from over $500.00 to
		over $1000.00.		
/*
Use y2k.pkg
REad_dfini "df23ini.cfg"
//----------------- Cannot Run Program From Command Line (DOS)
IF NEXT_MENU LE 0 ABORT
INTEGER USER_NUMB USER$_ACCESS   //must be first integers defined in program
STRING USER$ 30
MOVE '' TO USER$
MOVE 0 TO USER$_ACCESS
//------------------------------------------------------------
#include CalcDate.inc
#include macs.frm
//-------------- Find Logged On User For Program Use
OPEN USERX
CLEAR USERX
MOVE USER_NUMB TO USERX.RECNUM
FIND EQ USERX BY RECNUM
MOVE USERX.USER_NAME TO USER$
MOVE USERX.ACCS_LEVL TO USER$_ACCESS
//----------------------------------------------------


//---------------------------------------------------------------------- 
// Prints single warrant and information for each offense
//----------------------------------------------------------------------

STRING PROSECUTOR 32 PROS 30 OFFEN 1 answ 1 vitals 60
STRING CAPSCITY 25 FNAME 30 LNAME 30 DL 76 
STRING CAPSNAME 30 CITY 26 CLASSC 76 CLASSB 76 CLASSCOLD 76 CLASSBOLD 76
STRING COUNT 16 THE_JUDGE 31 JUDGE_NAME 30 CLASSD 76
STRING JUDGE_CITY 30 JUDGE_COUNT 16 COURT 9 MCOURT 9 FCOURT 9
NUMBER BOND HOLDID FBOND MBOND COSTS TOTAL
String TOT_JUDGE 31 JUDSE 1 PRINT_DATE 8 PAUSE 1 filedate$ 12
INTEGER YEAR DAY MONTH THIS_DAY THIS_YEAR 
STRING MONTH_NAME 9 THIS_MONTH 9
INTEGER WHERE FNDSW COUNT1 COUNT2
DATE TODAY COMPDATE COMPWARR 

MULTI$ OPEN SETUP JUDGE DEFENDNT RESTITUT 
OPEN OFFENSE INDEX.7
DIRECT_OUTPUT "LPT1:"
//DIRECT_OUTPUT "9ewar.txt"

STRING YR 4 SHORT_YR 4
SYSDATE4 DATE_SCRN.1
RIGHT DATE_SCRN.1 TO SHORT_YR 4
Move Short_yr to YR
DATE AGG_DATE

CLEAR SETUP
MOVE YR TO SETUP.YEAR
FIND EQ SETUP BY INDEX.1
[FINDERR] GOSUB NOSETUP
IF SETUP.PROSNAME LE '' GOSUB NOSETUP

  TRIM SETUP.PROSNAME TO PROSECUTOR
  MOVE PROSECUTOR TO PROS
  APPEND PROSECUTOR ","
  MOVE SETUP.FBOND TO FBOND
  MOVE SETUP.MBOND TO MBOND
  MOVE SETUP.COURT_COST TO COSTS
  MOVE SETUP.FEL_COURT TO FCOURT
  MOVE 'District' TO MCOURT
  INDICATE WBOND AS SETUP.FBOND GT 0

  MOVE " greater than $500.00 but less than $2500.00 making this a Class C felony.  " TO CLASSCOLD
  MOVE "             $2500.00 or greater making this a Class B felony.              " TO CLASSBOLD
  MOVE " greater than $1000.00 but less than $5000.01 making this a Class D felony. " TO CLASSD
  MOVE "greater than $5000.00 but less than $25,000.00 making this a Class C felony." TO CLASSC
  MOVE "            $25,000.00 or greater making this a Class B felony.             " TO CLASSB
  
  SYSDATE4 TODAY       
  MOVE TODAY TO PRINT_DATE 

  LEFT DATE_SCRN.1 TO MONTH 2 
  GOSUB FIND_MONTH
  MOVE MONTH_NAME TO THIS_MONTH

  MID DATE_SCRN.1 TO THIS_DAY 2 4 
  RIGHT DATE_SCRN.1 TO THIS_YEAR 4



PAGE SELECT
MOVE 'N' TO SELECT.1
ACCEPT SELECT.1                        {CAPSLOCK,CHECK='YN '}
ACCEPT SELECT.2                        {CAPSLOCK,CHECK='H '}
ACCEPT SELECT.3                        {CAPSLOCK,CHECK='L '}
MOVE 'N' TO SELECT.4
ACCEPT SELECT.4                        {CAPSLOCK,CHECK='YN '}
INDICATE FILE_DT AS SELECT.4 EQ 'Y'
[FILE_DT] BEGIN
   TRIM THIS_MONTH TO filedate$
   APPEND filedate$ ' ' THIS_DAY
END

[~ WBOND] BEGIN
    PAGE SELECT2
    ACCEPT SELECT2.1                        {CAPSLOCK,CHECK='YN '}
    INDICATE WBOND AS SELECT2.1 EQ 'Y'
END

MOVE 60 TO PAGEEND
MOVE 0 TO PAGEFEED
IF SELECT.3 EQ 'L' BEGIN
    MOVE 84 TO PAGEEND
    MOVE 84 TO PAGEFEED
END

IF SELECT.2 EQ 'H' MOVE -1 TO PAGEFEED

REPORT OFFENSE BY INDEX.7 

    IF OFFENSE.CKDT GT TODAY BEGIN
         MOVE OFFENSE.IDNO   TO BADDT.1
         MOVE OFFENSE.CTRLNO TO BADDT.2
         MOVE OFFENSE.CKDT   TO BADDT.3
         MOVE DATE_SCRN.1    TO BADDT.4
         INKEY PAUSE
         ABORT    //GOTO END.OF.REPORT
    END
         
         INDICATE SELECT AS OFFENSE.ACTIVE EQ ''
[~ SELECT] GOTO END.OF.REPORT
[SELECT] INDICATE SELECT AS OFFENSE.LETDT GT ''
[SELECT] IF OFFENSE.OFFEN EQ "H" BEGIN
			INDICATE SELECT AS OFFENSE.CKDT GE (TODAY - 1095)
			END
		 Else IF OFFENSE.OFFEN EQ "M" BEGIN
			INDICATE SELECT AS OFFENSE.CKDT GE (TODAY - 365)
		 END		
		 Else BEGIN
			INDICATE SELECT FALSE
		 END		
[SELECT] INDICATE SELECT AS OFFENSE.LETDT LE (TODAY - 14)
[SELECT] INDICATE SELECT AS OFFENSE.WARR_DATE EQ ''
[SELECT] RELATE OFFENSE

SECTION BODY //------------------------ Warrants ---------------------------
//    PAGECHECK 65
//[PAGEBREAK] FORMFEED
        IF DEFENDNT.LIC NE '' BEGIN
           MOVE 'D.L. No ' TO DL
           APPEND DL DEFENDNT.LIC
           APPEND DL ','
        END
        ELSE MOVE '' TO DL
        APPEND DL DEFENDNT.OTHER
        POS "," IN DEFENDNT.NAME TO WHERE 
        LEFT DEFENDNT.NAME TO LNAME (WHERE-1) 
        RIGHT DEFENDNT.NAME TO FNAME (30-WHERE) 
        TRIM FNAME TO FNAME 
        APPEND FNAME " " LNAME 
        GOTOXY 12 12
        SHOW 'PROCESSING WARRANT FOR ' FNAME '                         '
        [WBOND] PRINT 'BOND $' TO BODY.1
        [~ WBOND] BLANKFORM BODY.1 THRU BODY.2
        PRINT JUDGE.COUNT    TO BODY.5
        PRINT FNAME          TO BODY.7
        PRINT DEFENDNT.ADDR  TO BODY.8
        PRINT DEFENDNT.ADDR2 TO BODY.9
        PRINT DEFENDNT.CITY  TO BODY.10 
        PRINT DEFENDNT.ST    TO BODY.11
        MOVE ''		     TO	VITALS
        IF DEFENDNT.SOCSEC NE '' BEGIN
        	APPEND vitals 'SSN: '
        	APPEND vitals defendnt.socsec
        	APPEND vitals ', '
        END
        IF DEFENDNT.SEX	NE '' BEGIN
        	APPEND vitals 'SEX: '
        	APPEND vitals defendnt.sex
        	APPEND vitals ', '
        END
        IF DEFENDNT.RACE NE '' BEGIN
        	APPEND vitals 'RACE: '
        	APPEND vitals defendnt.race
        	APPEND vitals ', '
        END
        IF DEFENDNT.AGE NE '' BEGIN
        	APPEND vitals 'DOB: '
        	APPEND vitals defendnt.age
        END
        PRINT vitals	     TO BODY.12
        PRINT DL             TO BODY.13 
        TRIM JUDGE.NAME      TO THE_JUDGE
        APPEND THE_JUDGE ","
        TRIM JUDGE.CITY      TO CITY
        APPEND CITY ","
        PRINT CITY           TO BODY.15
        TRIM JUDGE.COUNT     TO COUNT
        APPEND COUNT ","
        MOVE JUDGE.NAME      TO JUDGE_NAME
        MOVE JUDGE.CITY      TO JUDGE_CITY
        MOVE JUDGE.COUNT     TO JUDGE_COUNT
        INDICATE FELONY FALSE
        INDICATE MISD FALSE
        BLANKFORM WITNESSES

//--------------- List bad checks, indicate felony or misd. -----
        PRINT OFFENSE.CKNO  TO CHECK_LINES.1
        PRINT OFFENSE.CKAMT TO CHECK_LINES.2
        PRINT OFFENSE.CKDT  TO CHECK_LINES.3
        PRINT OFFENSE.BANK  TO CHECK_LINES.4
        PRINT OFFENSE.NAME  TO CHECK_LINES.5
        PRINT OFFENSE.TAKEN TO WITNESSES.1
        PRINT OFFENSE.NAME  TO WITNESSES.2
        Reread offense
        MOVE TODAY TO OFFENSE.WARR_DATE   //----- Fill in Offense.Warr_Date----
        SAVERECORD OFFENSE
        Unlock
        MOVE OFFENSE.CKDT TO PRINT_DATE 
        LEFT PRINT_DATE   TO MONTH 2 
        GOSUB FIND_MONTH 
        MID PRINT_DATE    TO DAY 2 4 
        RIGHT PRINT_DATE  TO YEAR 2 
        INDICATE FELONY AS OFFENSE.OFFEN EQ 'H'
        INDICATE MISD AS OFFENSE.OFFEN EQ 'M'

        MOVE MCOURT TO COURT
        [FELONY] BEGIN
           MOVE FBOND TO BOND
           PRINT "Hot Check Felony     " TO BODY.14
           MOVE FCOURT TO COURT
           IF COURT MATCH 'Circuit' BEGIN
              INDICATE FELCT TRUE
              PRINT 'COUNTY'             TO BODY.4
              TRIM JUDGE.COUNT TO CAPSCITY 
              PRINT 'County'             TO BODY.20
              PRINT COUNT                TO BODY.21
              PRINT 'County'             TO BODY.61
           END
           ELSE BEGIN
              INDICATE FELCT FALSE
              PRINT 'CITY'               TO BODY.4
              TRIM JUDGE.CITY TO CAPSCITY 
              PRINT 'City'               TO BODY.20
              PRINT CITY                 TO BODY.21
              PRINT 'City'               TO BODY.61
           END
           
          END
        [~ FELONY MISD] BEGIN
           MOVE MBOND TO BOND
           PRINT "Hot Check Misdemeanor" TO BODY.14
           MOVE MCOURT TO COURT
           PRINT 'CITY'                  TO BODY.4
           TRIM JUDGE.CITY TO CAPSCITY 
           PRINT 'City'                  TO BODY.20
           PRINT CITY                    TO BODY.21
           PRINT 'City'                  TO BODY.61
        END
        [~ FELONY ~ MISD] MOVE 0 TO BOND
        IF BOND GT 0 PRINT BOND          TO BODY.2
        PRINT COURT                      TO BODY.3
        PRINT COURT                      TO BODY.19
        PRINT COURT                      TO BODY.29
        PRINT COURT                      TO BODY.34
        PRINT COURT                      TO BODY.39
        PRINT COURT                      TO BODY.60
        APPEND CAPSCITY ", ARKANSAS"
        PRINT CAPSCITY                   TO BODY.6
        PRINT COUNT                      TO BODY.16
        PRINT FNAME                      TO BODY.17
        PRINT THE_JUDGE                  TO BODY.18
        PRINT THIS_DAY                   TO BODY.22
        PRINT THIS_MONTH                 TO BODY.23
        PRINT THIS_YEAR                  TO BODY.24
        PRINT WITNESSES.1                TO BODY.25
        PRINT WITNESSES.2                TO BODY.26
        [FILE_DT] BEGIN
          PRINT filedate$                 TO BODY.36
          PRINT THIS_YEAR                TO BODY.37
        END
        OUTPUT BODY
        FORMFEED

[FELONY] GOSUB INFO1                        //---- goto print felony info -----
[~ FELONY] IF SELECT.1 EQ 'Y' GOSUB INFO2  //---- goto print misd. info ------

REPORTEND 
ABORT 

//=============================================================================
//===============================  Subroutines  ===============================
//=============================================================================
 
INFO1:                                                    // Felony Information
       PRINT JUDGE_NAME    TO FELONY1.1
       PRINT COURT         TO FELONY1.2
       [FELCT] BEGIN
         PRINT 'County'    TO FELONY1.4
         PRINT JUDGE.COUNT TO FELONY1.5
       END
       [~ FELCT] BEGIN
         PRINT '  City'    TO FELONY1.4
         PRINT JUDGE_CITY  TO FELONY1.5
       END
       PRINT OFFENSE.CKAMT TO FELONY1.6
       PRINT OFFENSE.MFEE  TO FELONY1.7
       PRINT OFFENSE.PFEE  TO FELONY1.9
       IF COSTS GT 0 BEGIN
         PRINT COSTS       TO FELONY1.10
         PRINT (FELONY1.6 + FELONY1.7 + FELONY1.9 + FELONY1.10) TO FELONY1.12
       END
       ELSE BEGIN
         CLEARFORM FELONY1.10
         CLEARFORM FELONY1.12
       END
       PRINT FNAME         TO FELONY1.8
       PRINT JUDGE_COUNT   TO FELONY1.11
       PRINT PROSECUTOR    TO FELONY1.13
       PRINT SETUP.DIST    TO FELONY1.14
       PRINT FNAME         TO FELONY1.15
       PRINT JUDGE_COUNT   TO FELONY1.16
       PRINT CHECK_LINES.1 TO FELONY1.17
       PRINT CHECK_LINES.2 TO FELONY1.18
       PRINT CHECK_LINES.3 TO FELONY1.19
       PRINT CHECK_LINES.4 TO FELONY1.20
       PRINT CHECK_LINES.5 TO FELONY1.21
       PRINT CHECK_LINES.2 TO FELONY1.22
	   IF OFFENSE.CKDT LT "07/27/2011" BEGIN	   
		  IF CHECK_LINES.2 LE 2500.00 PRINT CLASSCOLD TO FELONY1.23
          ELSE PRINT CLASSBOLD TO FELONY1.23
       END
	   ELSE BEGIN 
	      IF CHECK_LINES.2 LE 5000.00 PRINT CLASSD TO FELONY1.23
	      ELSE IF CHECK_LINES.2 LE 25000.00 PRINT CLASSC TO FELONY1.23
          ELSE PRINT CLASSB TO FELONY1.23
	   END		
       PRINT WITNESSES.1   TO FELONY1.24
       PRINT PROS          TO FELONY1.25
       PRINT WITNESSES.2   TO FELONY1.26
       [FILE_DT] BEGIN
         PRINT THIS_DAY    TO FELONY1.33
         Print This_MONTH  TO FELONY1.34
         PRINT THIS_YEAR   TO FELONY1.35
       END
       OUTPUT FELONY1
       FORMFEED
RETURN

INFO2:                                                //Misdemeanor Information
       PRINT JUDGE_NAME    TO MISDEMEANOR1.1
       PRINT JUDGE_CITY    TO MISDEMEANOR1.3
       PRINT OFFENSE.CKAMT TO MISDEMEANOR1.4
       PRINT OFFENSE.MFEE  TO MISDEMEANOR1.5
       PRINT OFFENSE.PFEE  TO MISDEMEANOR1.7
       IF COSTS GT 0 BEGIN
         PRINT COSTS TO MISDEMEANOR1.8
         CALC (MISDEMEANOR1.4 + MISDEMEANOR1.5 + MISDEMEANOR1.7) TO TOTAL
         PRINT (TOTAL + MISDEMEANOR1.8) TO MISDEMEANOR1.10
       END
       ELSE BEGIN
         CLEARFORM MISDEMEANOR1.8
         CLEARFORM MISDEMEANOR1.10
       END
       PRINT FNAME         TO MISDEMEANOR1.6
       PRINT JUDGE_COUNT   TO MISDEMEANOR1.9
       PRINT PROSECUTOR    TO MISDEMEANOR1.11
       PRINT SETUP.DIST    TO MISDEMEANOR1.12
       PRINT FNAME         TO MISDEMEANOR1.13
       PRINT JUDGE_COUNT   TO MISDEMEANOR1.14
       PRINT CHECK_LINES.1 TO MISDEMEANOR1.15
       PRINT CHECK_LINES.2 TO MISDEMEANOR1.16
       PRINT CHECK_LINES.3 TO MISDEMEANOR1.17
       PRINT CHECK_LINES.4 TO MISDEMEANOR1.18
       PRINT CHECK_LINES.5 TO MISDEMEANOR1.19
       PRINT PROS          TO MISDEMEANOR1.20
       PRINT WITNESSES.1   TO MISDEMEANOR1.21
       PRINT WITNESSES.2   TO MISDEMEANOR1.22
       [FILE_DT] BEGIN
         PRINT THIS_DAY    TO MISDEMEANOR1.27
         PRINT THIS_MONTH  TO MISDEMEANOR1.28
         PRINT THIS_YEAR   TO MISDEMEANOR1.29
       END
       OUTPUT MISDEMEANOR1
       FORMFEED
RETURN

FIND_MONTH: 
       IF MONTH EQ 1 MOVE " January "  TO MONTH_NAME  
       IF MONTH EQ 2 MOVE "February "  TO MONTH_NAME 
       IF MONTH EQ 3 MOVE "  March  "  TO MONTH_NAME 
       IF MONTH EQ 4 MOVE "  April  "  TO MONTH_NAME 
       IF MONTH EQ 5 MOVE "   May   "  TO MONTH_NAME 
       IF MONTH EQ 6 MOVE "   June  "  TO MONTH_NAME 
       IF MONTH EQ 7 MOVE "   July  "  TO MONTH_NAME 
       IF MONTH EQ 8 MOVE "  August "  TO MONTH_NAME 
       IF MONTH EQ 9 MOVE "September"  TO MONTH_NAME 
       IF MONTH EQ 10 MOVE " October " TO MONTH_NAME 
       IF MONTH EQ 11 MOVE "November " TO MONTH_NAME 
       IF MONTH EQ 12 MOVE "December " TO MONTH_NAME 
RETURN 

KEYPROC KEY.ESCAPE
  ABORT

NOSETUP:
      MOVE YR TO NOSETUP.1
      INKEY PAUSE
      ABORT
RETURN
